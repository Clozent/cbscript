dir "C:\Users\Seth\AppData\Roaming\.minecraft\saves\SethBling's World 1.13"
desc "Grows realistic trees through bifurcation."

import common

define @Branch = @Entity[type=area_effect_cloud, tag=Branch]
	create {Tags:["Branch"], Duration:-1,Age:-2147483648,WaitTime:-2147483648}
end

define @Position = @Entity[type=area_effect_cloud,tag=Position,limit=1]
	create {Tags:["Position"], Duration:-1,Age:-2147483648,WaitTime:-2147483648}
end

reset
	at @Branch
		/setblock ~ ~ ~ air
	end
	/kill @Branch
	
	tell @a "{G[\[Create Tree\]](/function bifurcation:create_tree)"
end

function create_tree()
	at @s
		as create @Branch
			@s.root = True
			@s.<growth> = <0, 1000, 0>
			@s.leaf = True
		end
	end
end

clock main
	as @Branch at @s
		/setblock ~ ~ ~ oak_wood
		unless @Position do create @Position
		@Position.<pos> = @s.<pos> + @s.<growth>
		if @s.leaf
			at @Position
				/setblock ~ ~ ~ oak_leaves
			end
		end
	end
	
	growth_clock++
	if growth_clock >= 20
		growth_clock = 0
		
		as @Branch[leaf]
			@s.leaf = False
			<growth> = @s.<growth>
			at @s unless @Position do create @Position
			@Position.<pos> = @s.<pos> + @s.<growth>
			at @Position
				for $i in [0, 1]
					as create @Branch
						@s.<growth> = <growth> + <rand(-500, 500), rand(-500, 500), rand(-500, 500)>
						@s.leaf = True
					end
				end
			end
		end
	end
end