dir: C:\Users\Seth\AppData\Roaming\.minecraft\saves\SethBling's World 1.13
desc: A camera with shading
scale: 1000

define @Entity = @e
	x = Pos[0] double
	y = Pos[1] double
	z = Pos[2] double
	<pos> = Pos double
	vx = Motion[0] double
	vy = Motion[1] double
	vz = Motion[2] double
	ry = Rotation[0] float
	rx = Rotation[1] float
	on_ground = OnGround byte 1
	no_gravity = NoGravity byte 1
end

define @Scan = @Entity[type=area_effect_cloud,tag=Scan,limit=1]
	create: {Tags:["Scan"], Duration:-1,Age:-2147483648,WaitTime:-2147483648}
end

define @Position = @Entity[type=area_effect_cloud,tag=Position,limit=1]
	create: {Tags:["Position"], Duration:-1,Age:-2147483648,WaitTime:-2147483648}
end


$Pixels: 128
$Radius: 10
$MinusRadius: -Radius
$Diameter: 2 * Radius
$MinusDiameter: -Diameter
$PixelMove: float(Diameter) / Pixels
$MinusPixelMove: -PixelMove

define block none
	air
	cave_air
	grass
	tall_grass
end

define block grass
	grass_block
end

define block sand
	sand
	sandstone
end

define block cloth
	cobweb
end

define block tnt
	tnt
end

define block ice
	ice
	blue_ice
	packed_ice
end

define block iron
	iron_block
end

define block foliage
	oak_leaves
end

define block snow
	snow
	snow_block
end

define block clay
	clay
end

define block dirt
	dirt
end

define block stone
	stone
end

#define block WATER

define block wood
	oak_planks
	oak_log
end

define block quartz
	quartz_block
end

define block adobe
	acacia_planks
end

define block magenta
	magenta_wool
end

define block light_blue
	light_blue_wool
end

define block yellow
	yellow_wool
end

define block lime
	lime_wool
end

define block pink
	pink_wool
end

define block gray
	gray_wool
end

define block silver
	light_gray_wool
end

define block cyan
	cyan_wool
end

define block purple
	purple_wool
end

define block blue
	blue_wool
	water
end

define block brown
	brown_wool
end

define block green
	green_wool
end

define block red
	red_wool
end

define block black
	black_wool
end

define block gold
	gold_block
end

define block diamond
	diamond_block
end

define block lapis
	lapis_block
end

define block emerald
	emerald_block
end

define block obsidian
	obsidian
end

define block netherrack
	netherrack
end

define block white_stained_hardened_clay
	white_glazed_terracotta
end

define block orange_stained_hardened_clay
	orange_glazed_terracotta
end

define block magenta_stained_hardened_clay
	magenta_glazed_terracotta
end

define block light_blue_stained_hardened_clay
	light_blue_glazed_terracotta
end

define block yellow_stained_hardened_clay
	yellow_glazed_terracotta
end

define block lime_stained_hardened_clay
	lime_glazed_terracotta
end

define block pink_stained_hardened_clay
	pink_glazed_terracotta
end

define block gray_stained_hardened_clay
	gray_glazed_terracotta
end

define block silver_stained_hardened_clay
	light_gray_glazed_terracotta
end

define block cyan_stained_hardened_clay
	cyan_glazed_terracotta
end

define block purple_stained_hardened_clay
	purple_glazed_terracotta
end

define block blue_stained_hardened_clay
	blue_glazed_terracotta
end

define block brown_stained_hardened_clay
	brown_glazed_terracotta
end

define block green_stained_hardened_clay
	green_glazed_terracotta
end

define block red_stained_hardened_clay
	red_glazed_terracotta
end

define block black_stained_hardened_clay
	black_glazed_terracotta
end

macro $replace_block()
	if block ~ ~ ~ grass
		/setblock 0 1 0 grass_block
	end
	if block ~ ~ ~ sand
		/setblock 0 1 0 sandstone
	end
	if block ~ ~ ~ cloth
		/setblock 0 1 0 cobweb
	end
	if block ~ ~ ~ tnt
		/setblock 0 1 0 tnt
	end
	if block ~ ~ ~ ice
		/setblock 0 1 0 ice
	end
	if block ~ ~ ~ iron
		/setblock 0 1 0 iron_block
	end
	if block ~ ~ ~ foliage
		/setblock 0 1 0 oak_leaves
	end
	if block ~ ~ ~ snow
		/setblock 0 1 0 snow_block
	end
	if block ~ ~ ~ clay
		/setblock 0 1 0 clay
	end
	if block ~ ~ ~ dirt
		/setblock 0 1 0 dirt
	end
	if block ~ ~ ~ stone
		/setblock 0 1 0 stone
	end
	if block ~ ~ ~ wood
		/setblock 0 1 0 oak_planks
	end
	if block ~ ~ ~ quartz
		/setblock 0 1 0 quartz_block
	end
	if block ~ ~ ~ adobe
		/setblock 0 1 0 acacia_planks
	end
	if block ~ ~ ~ magenta
		/setblock 0 1 0 magenta_wool
	end
	if block ~ ~ ~ light_blue
		/setblock 0 1 0 light_blue_wool
	end
	if block ~ ~ ~ yellow
		/setblock 0 1 0 yellow_wool
	end
	if block ~ ~ ~ lime
		/setblock 0 1 0 lime_wool
	end
	if block ~ ~ ~ pink
		/setblock 0 1 0 pink_wool
	end
	if block ~ ~ ~ gray
		/setblock 0 1 0 gray_wool
	end
	if block ~ ~ ~ silver
		/setblock 0 1 0 light_gray_wool
	end
	if block ~ ~ ~ cyan
		/setblock 0 1 0 cyan_wool
	end
	if block ~ ~ ~ purple
		/setblock 0 1 0 purple_wool
	end
	if block ~ ~ ~ blue
		/setblock 0 1 0 blue_wool
	end
	if block ~ ~ ~ brown
		/setblock 0 1 0 brown_wool
	end
	if block ~ ~ ~ green
		/setblock 0 1 0 green_wool
	end
	if block ~ ~ ~ red
		/setblock 0 1 0 red_wool
	end
	if block ~ ~ ~ black
		/setblock 0 1 0 black_wool
	end
	if block ~ ~ ~ gold
		/setblock 0 1 0 gold_block
	end
	if block ~ ~ ~ diamond
		/setblock 0 1 0 diamond_block
	end
	if block ~ ~ ~ lapis
		/setblock 0 1 0 lapis_block
	end
	if block ~ ~ ~ emerald
		/setblock 0 1 0 emerald_block
	end
	if block ~ ~ ~ obsidian
		/setblock 0 1 0 obsidian
	end
	if block ~ ~ ~ netherrack
		/setblock 0 1 0 netherrack
	end
	if block ~ ~ ~ white_stained_hardened_clay
		/setblock 0 1 0 white_glazed_terracotta
	end
	if block ~ ~ ~ orange_stained_hardened_clay
		/setblock 0 1 0 orange_glazed_terracotta
	end
	if block ~ ~ ~ magenta_stained_hardened_clay
		/setblock 0 1 0 magenta_glazed_terracotta
	end
	if block ~ ~ ~ light_blue_stained_hardened_clay
		/setblock 0 1 0 light_blue_glazed_terracotta
	end
	if block ~ ~ ~ yellow_stained_hardened_clay
		/setblock 0 1 0 yellow_glazed_terracotta
	end
	if block ~ ~ ~ lime_stained_hardened_clay
		/setblock 0 1 0 lime_glazed_terracotta
	end
	if block ~ ~ ~ pink_stained_hardened_clay
		/setblock 0 1 0 pink_glazed_terracotta
	end
	if block ~ ~ ~ gray_stained_hardened_clay
		/setblock 0 1 0 gray_glazed_terracotta
	end
	if block ~ ~ ~ silver_stained_hardened_clay
		/setblock 0 1 0 light_gray_glazed_terracotta
	end
	if block ~ ~ ~ cyan_stained_hardened_clay
		/setblock 0 1 0 cyan_glazed_terracotta
	end
	if block ~ ~ ~ purple_stained_hardened_clay
		/setblock 0 1 0 purple_glazed_terracotta
	end
	if block ~ ~ ~ blue_stained_hardened_clay
		/setblock 0 1 0 blue_glazed_terracotta
	end
	if block ~ ~ ~ brown_stained_hardened_clay
		/setblock 0 1 0 brown_glazed_terracotta
	end
	if block ~ ~ ~ green_stained_hardened_clay
		/setblock 0 1 0 green_glazed_terracotta
	end
	if block ~ ~ ~ red_stained_hardened_clay
		/setblock 0 1 0 red_glazed_terracotta
	end
	if block ~ ~ ~ black_stained_hardened_clay
		/setblock 0 1 0 black_glazed_terracotta
	end
end

reset
	/gamerule maxCommandChainLength 1000000000
	
    /bossbar add progress "Progress"
    /bossbar set progress max 128

	unless @Scan do create @Scan
	unless @Position do create @Position

	tell @a: {Y[\[Take Picture\]](/function shaded_camera:take_picture) [\[Setup\]](/function shaded_camera:setup)
end

function setup()

end

function take_picture()
	/bossbar set progress players @s
              
	at @s
		/tp @Scan ~ ~1.6 ~ ~ ~
	end
	
	move @Scan ^$Radius ^$Radius ^10
	at -705 56 64
		col = 0
		while col < 128 at ~1 128 ~
			# Recursively capture column pixels
			row = 0
			/execute store result bossbar progress value run scoreboard players get Global col
			take_picture_col()
			
			col++
			move @Scan ^$MinusPixelMove ^$Diameter ^
			<x, y, z> = @Scan.<pos>
		end
	end
	/bossbar set progress players
end

macro $take_picture_col_recurse($dy)
	at ~ ~$dy ~
		# Copy the block into the map area
		/clone 0 1 0 0 1 0 ~ ~ ~
		/fill ~ ~1 ~ ~ 255 ~ air
		
		# Move on to the next row
		row++
		move @Scan ^ ^$MinusPixelMove ^
		<x, y, z> = @Scan.<pos>
		if row < 128 at ~ ~ ~1
			take_picture_col()
		end
	end
end

function take_picture_col()
	# Perform recursive ray trace
	step = 0
	at @s ~ ~1.6 ~ facing @Scan
		ray_trace_step()
	end
	
	handled = False
	if face_x
		$take_picture_col_recurse(-1)
		handled = True
	end
	
	if face_y and not handled
		$take_picture_col_recurse(0)
		handled = True
	end
	
	if face_z and not handled
		$take_picture_col_recurse(1)
		handled = True
	end
end

function ray_trace_step()
	$Step: 0.25
	$MinusStep: -Step
	handled = False
	if block ~ ~ ~ none and step < 200
		step++
		at ^ ^ ^$Step do ray_trace_step()
		handled = True
	end
	
	if not handled
		at ^ ^ ^$MinusStep
			fine_step = 0
			ray_trace_fine_step()
		end
	end
end

function ray_trace_fine_step()
	$FineStep: 0.02
	$MinusFineStep: -FineStep

	handled = False
	if block ~ ~ ~ none and fine_step < 50
		fine_step++
		at ^ ^ ^$FineStep do ray_trace_fine_step()
		handled = True
	end
	
	if not handled
		$replace_block()
		if fine_step >= 50
			/setblock 0 1 0 light_blue_wool
		end
		
		# Compute which face was crossed first		
		as @Position
			/tp @s ~ ~ ~ ~ ~
			<x2, y2, z2> = @s.<pos>
			move @s ^ ^ ^$MinusFineStep
			<x1, y1, z1> = @s.<pos>
		end
		<bx1, by1, bz1> = <x1, y1, z1> % 1000
		<dx, dy, dz> = <x2, y2, z2> - <x1, y1, z1>
		<bx2, by2, bz2> = <bx1, by1, bz1> + <dx, dy, dz>
		
		if bx2 > 1000
			px = (1000-bx1) * 100 / dx
		end
		if bx2 < 0
			px = bx1 * 100 / -dx
		end
		if bx2 >= 0 and bx2 < 1000
			px = 0
		end
		
		if by2 > 1000
			py = (1000-by1) * 100 / dy
			top_face = False
			bottom_face = True
		end
		if by2 < 0
			py = by1 * 100 / -dy
			top_face = True
			bottom_face = False
		end
		if by2 >= 0 and by2 < 1000
			py = 0
		end
		
		if bz2 > 1000
			pz = (1000-bz1) * 100 / dz
		end
		if bz2 < 0
			pz = bz1 * 100 / -dz
		end
		if bz2 >= 0 and bz2 < 1000
			pz = 0
		end
		
		if face_x and block ~ ~ ~ grass_block
			/setblock 0 1 0 dirt
		end
		if face_z and block ~ ~ ~ grass_block
			/setblock 0 1 0 dirt
		end
		if face_y and bottom_face and block ~ ~ ~ grass_block
			/setblock 0 1 0 dirt
		end
		
		if px >= py and px >= pz
			face_x = True
			face_y = False
			face_z = False
		end
		
		if py >= px and py >= pz
			face_x = False
			face_y = True
			face_z = False
		end
		
		if pz >= px and pz >= py
			face_x = False
			face_y = False
			face_z = True
		end
	end
end

